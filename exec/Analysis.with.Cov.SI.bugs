model{

for(ps in 1:n.prey.samps.SI){
    preys.SI[ps,1:isos] ~ dmnorm(prey.means_SI[prey.ix.SI[ps],1:isos],prey.precs_SI[prey.ix.SI[ps],,]) 
  }

  for (j in 1:n.preys){
        
    prey.precs_SI[j,1:isos,1:isos] ~ dwish(R_SI[,,j],ni.SI[j])
    prey.means_SI[j,1:isos] ~  dmnorm(preym.SI[j,],Rnot_SI[,])
    cons.prey_SI[j,1:isos] ~ dmnorm(prey.means_SI[j,1:isos],prey.precs_SI[j,,])

    for (i in 1:isos)
    { 
      cs[j,i] ~ dnorm(mean_cs[j,i],tau_cs[j,i])
      prey_SI[j,i] <- cs[j,i]+cons.prey_SI[j,i]
    }
 
  }

  for(i in 1:n.preds) {      
    
       # draw clr(proportions)
mus[i,1:m.preys] <- beta[1:m.preys,1:n.covs] %*% Covs[i,1:n.covs]
  pnorm[i,1:m.preys] ~ dmnorm(mus[i,],pprec[,])    

    for(j in 1:m.preys) {
      
      # backtransform clr proportions by clr^-1
      p_unn[i,j] <- exp(pnorm[i,j])
          
    }
    
p_unn[i,n.preys] <- 1
prop[i,1:n.preys] <- (p_unn[i,1:n.preys])/sum(p_unn[i,1:n.preys])

   # predator likelihood
    
    preds.SI[i,1:isos] ~ dmnorm(mu_SI[i,],predprec_SI[,])
 
  # mixing for predator SI signature for likelihood
 
  mu_SI[i,1:isos] <- prop[i,1:n.preys] %*% prey_SI[1:n.preys,]
    
  
  }   
        

  # priors for proportions and predator covariance
   predprec_SI[1:isos,1:isos] ~ dwish(Rnot_SI[,],isos)
  
for(k in 1:n.covs){  
      beta[1:m.preys,k] ~ dmnorm(zeros[],S[,])
      for(j in 1:m.preys) { 
  
    	      pm_unn[j,k] <- exp(beta[j,1]+beta[j,k]*ind[k])    
	      #closure   
	      }
	pm_unn[n.preys,k] <- 1
	pop.prop[1:n.preys,k] <- (pm_unn[1:n.preys,k])/sum(pm_unn[1:n.preys,k])
}
 pprec[1:m.preys,1:m.preys] ~ dwish(SS[,],m.preys)
}
