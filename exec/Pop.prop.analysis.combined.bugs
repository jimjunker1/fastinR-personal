model{
 for(p in 1:n.prey.samps){
    preys[p,1:m.fats] ~ dmnorm(prey.means[prey.ix[p],1:m.fats],prey.precs[prey.ix[p],,]) 
  }
  for(ps in 1:n.prey.samps.SI){
    preys.SI[ps,1:isos] ~ dmnorm(prey.means_SI[prey.ix.SI[ps],1:isos],prey.precs_SI[prey.ix.SI[ps],,]) 
  }

  for (j in 1:n.preys){
    
    prop[j] <- ps[j]/sum(ps[1:n.preys] )
    ps[j] ~ dgamma(1/n.preys,1)I(0.01,)
    
    # draw fat content from prior
    fc[j] ~ dlnorm(fc_mean[j],fc_tau[j])
    
    prey.precs[j,1:m.fats,1:m.fats] ~ dwish(R[1:m.fats,1:m.fats,j],ni[j])
    prey.means[j,1:m.fats] ~  dmnorm(preym[j,],Rnot[,])      
    cons.prey[j,1:m.fats] ~  dmnorm(prey.means[j,],prey.precs[j,,])  
    # same for SI
    prey.precs_SI[j,1:isos,1:isos] ~ dwish(R_SI[,,j],ni.SI[j])
    prey.means_SI[j,1:isos] ~  dmnorm(preym.SI[j,],Rnot_SI[,])
    cons.prey_SI[j,1:isos] ~ dmnorm(prey.means_SI[j,1:isos],prey.precs_SI[j,,])

    for (i in 1:isos)
    { 
      cs[j,i] ~ dnorm(mean_cs[j,i],tau_cs[j,i])
      prey_SI[j,i] <- cs[j,i]+cons.prey_SI[j,i]
    }
    
    for (f in 1:m.fats){
      
      # backtransform to apply fractionation and fat content
      preydd[j,f] <- exp(cons.prey[j,f])
      preyd[j,f] <- preydd[j,f]/(sum(preydd[j,1:m.fats])+1)

# draw fractionation coeffs for prey j and fatty acid f
      pre.fracs[j,f] ~ dgamma(mean_c[j,f],tau_coeffs[j,f])
      fracs[j,f] <- pre.fracs[j,f]/sum(pre.fracs[j,1:n.fats])
#apply all
      preyf[j,f] <- (preyd[j,f]*fracs[j,f]*fc[j])
      
    }
    
    preyd[j,n.fats] <- 1/(sum(preydd[j,1:m.fats])+1)
    # draw fractionation coeffs for prey j and fatty acid f
      pre.fracs[j,n.fats] ~ dgamma(mean_c[j,n.fats],tau_coeffs[j,n.fats])
      fracs[j,n.fats] <- pre.fracs[j,n.fats]/sum(pre.fracs[j,1:n.fats])
      preyf[j,n.fats] <- (preyd[j,n.fats]*fracs[j,n.fats]*fc[j])

    
  }
  
  for(i in 1:n.preds) {     
       
    # predator likelihood
    preds[i,1:m.fats] ~ dmnorm(mu[],predprec[,])
    
    preds.SI[i,1:isos] ~ dmnorm(mu_SI[],predprec_SI[,])
      
    }
 
  # mixing for predator SI signature for likelihood
  
    mu_SI[1:isos] <- prop[1:n.preys] %*% prey_SI[1:n.preys,]
    
  
  
  # mixing and alr transformation of predator fat signature for likelihood
 
    predm[1:n.fats] <- prop[1:n.preys] %*% preyf[1:n.preys,]
    predf[1:n.fats] <- predm[1:n.fats]/sum(predm[1:n.fats])


    mu[1:m.fats] <- log(predf[1:m.fats]/predf[n.fats])
    
  # priors for proportions and predator covariance
  
  predprec[1:m.fats,1:m.fats] ~ dwish(Rnot[,],m.fats)
  predprec_SI[1:isos,1:isos] ~ dwish(Rnot_SI[,],isos)
  
}
